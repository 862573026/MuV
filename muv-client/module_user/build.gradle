apply from: '../micro.gradle'
apply plugin: 'org.greenrobot.greendao' // apply plugin

if (isModule.toBoolean()) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}

android {
    compileSdkVersion build_versions.target_sdk

    defaultConfig {
        minSdkVersion build_versions.min_sdk
        targetSdkVersion build_versions.target_sdk
        versionCode 1
        versionName "1.0"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [ moduleName : project.getName() ]
            }
        }

        multiDexEnabled true
    }

    sourceSets {
        main {
            java.srcDirs = p_src_dirs
            res.srcDirs = p_res_dirs
            jniLibs.srcDirs = p_jniLibs_dirs

            if (isModule.toBoolean()) {
                manifest.srcFile 'src/main/module/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    dataBinding {
        enabled = true
    }

    buildTypes {
        release {
            if (isModule.toBoolean()) {
                // 缩减resource文件
                shrinkResources true
            }
            //是否zip对齐
            zipAlignEnabled true
            //Proguard
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            //给applicationId添加后缀“.debug”
            if (isModule.toBoolean()) {
                applicationIdSuffix ".debug"
                shrinkResources false
            }
            zipAlignEnabled false
            shrinkResources false
            minifyEnabled false
            debuggable true
        }
    }
}

greendao {
    //数据库的schema版本，也可以理解为数据库版本号
    schemaVersion 1
    //设置DaoMaster、DaoSession、Dao包名，也就是要放置这些类的包的全路径
    daoPackage 'com.newx.user.entity.gen'
    //设置DaoMaster、DaoSession、Dao目录
    targetGenDir 'src/main/java'
}

dependencies {
    api fileTree(dir: 'libs', include: ['*.jar'])

    api project(':lib-common')
    annotationProcessor project(':lib-compiler')
    implementation deps.greendao
}
